plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.mysite'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Flyway (DB Migration)
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // SMTP Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // Spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    testImplementation 'org.springframework.security:spring-security-test'

    // Thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Database Drivers
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Sql Logging
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.11.0'

    // SpringDoc OpenAPI Starter WebMVC UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.12.6"

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    // AWS SDK for S3
    implementation 'software.amazon.awssdk:s3:2.31.77'

    // AWS SDK for CloudFront (Signed URL/Cookie)
    implementation 'software.amazon.awssdk:cloudfront:2.31.77'


    // MongoDB
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
}

tasks.named('test') {
    useJUnitPlatform()
}


jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, include: [
                'com/mysite/sns1_server/domain/**/service/**',
                'com/mysite/sns1_server/domain/**/controller/**'
        ])
    }))
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, include: [
                'com/mysite/sns1_server/domain/**/service/**',
                'com/mysite/sns1_server/domain/**/controller/**'
        ])
    }))
}

tasks.named('build') {
    dependsOn jacocoTestCoverageVerification
}

def generatedQuerydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDirs += generatedQuerydslDir
}

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = file(generatedQuerydslDir)
}

